apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.ver_code
        versionName versions.ver_name
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions.freeCompilerArgs += ['-module-name', 'easycommon-mvp']

    afterEvaluate {
        generateReleaseBuildConfig.enabled = false
        generateDebugBuildConfig.enabled = false
    }
}

dependencies {
    api deps.support.appcompat
    api deps.rxJava
    testImplementation deps.test.junit
    androidTestImplementation deps.test.android
}


tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

//// build a jar with source files
task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.source
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        links "https://developer.android.com/reference"
        title "mvp"
    }
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    afterEvaluate {
        android.libraryVariants.all { variant ->
            variant.javaCompileProvider.configure {
                javadoc.classpath += files(it.classpath.files)
            }
        }
    }
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
